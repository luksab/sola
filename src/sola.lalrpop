use std::str::FromStr;
use crate::base_ast::*;
use lalrpop_util::ErrorRecovery;

grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, Token<'input>, SolaParseError>>);
// grammar;
extern {
    type Error = SolaParseError;
}

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Tier<Op,NextTier>: Box<Expression<'input>> = {
    Tier<Op,NextTier> Op NextTier => Box::new(Expression::Op(<>)),
    NextTier
};

// Main expression grammar

pub Program: Program<'input> = {
    (<TopLevel>)* => Program{things: <>}
}

pub TopLevel: TopLevel<'input> = {
    // <Declaration> => TopLevel::Declaration(<>),
    <Function> => TopLevel::Function(<>),
    Comment => TopLevel::Comment(<>),
    // <Expression> => TopLevel::Expression(<>),
    // <Statement> => TopLevel::Statement(<>),
};

pub Comment: Comment<'input> = {
    r"//[^\n\r]*" => Comment::new(<>),
};

Identifier: &'input str = {
    r"[a-zA-Z\{Emoji}][\w\{Emoji}]*"
};

// Function grammar

Function: Function<'input> = {
    "extern fn" <definition: FunctionDefinition> => Function {
        definition: definition,
        body: None,
    },
    "fn " <definition: FunctionDefinition> <body:Block> => Function {
        definition: definition,
        body: Some(Box::new(Expression::Block(body))),
    },
};

FunctionDefinition: FunctionDefinition<'input> = {
    <name:Identifier> "(" <params:(Parameter)*> ")" "->" <return_type: Type> => FunctionDefinition {
        name: name,
        params: params,
        return_type: Some(return_type),
    },
    <name:Identifier> "(" <params:(Parameter)*> ")" => FunctionDefinition {
        name: name,
        params: params,
        return_type: None,
    }
};

Parameter: Parameter<'input> = {
    <name:Identifier>":" <tipe: Type> => Parameter {
        name: name,
        tipe: tipe,
    }
};

Type: Type<'input> = {
    "i32" => Type::I32,
    "bool" => Type::Bool,
};

// Expression grammar
Statement: Statement<'input> = {
    "let " <name:Identifier>":" <t:Type> "=" <e:Expression> ";" => 
        Statement::Let(Let{name, tipe: t, value: e}),
    "return" <e:Expression> ";" => Statement::Return(Some(e)),
    "return" ";" => Statement::Return(None),
    // "if" <e:Expression> "{" <b:Block> "}" => {
    //     Statement::If(If {
    //         condition: e, body: Box::new(Expression::Block(b)), else_body: None
    //     })
    // },
    // "if" <e:Expression> "{" <b:Block> "}" "else" "{" <be:Block> "}" => {
    //     Statement::Expression(Box::new(Expression::If(If {
    //         condition: e, body: Box::new(Expression::Block(b)), else_body: Some(Box::new(Expression::Block(be)))
    //     })))
    // },
    <e:Expression> ";" => Statement::Expression(e),
    Comment => Statement::Comment(<>),
    ! => { errors.push(<>); Statement::Error },
};

Expr: Box<Expression<'input>> = {
    Literal => Box::new(Expression::Literal(<>)),
    <s:r#""[^"]*""#> => Box::new(Expression::String(ASTString{value: <>})),
    "(" <Expression> ")",
    "if" <e:Expression> <b:Block> "else" <be:Block> => {
        Box::new(Expression::If(If {
            condition: e, body: Box::new(Expression::Block(b)), else_body: Some(Box::new(Expression::Block(be)))
        }))
    },
    "if" <e:Expression> <b:Block> => {
        Box::new(Expression::If(If {
            condition: e, body: Box::new(Expression::Block(b)), else_body: None
        }))
    },
    "while" <e:Expression> <b:Block> => {
        Box::new(Expression::While(While {
            condition: e, body: Box::new(Expression::Block(b))
        }))
    },
    <FunctionCall> => Box::new(Expression::FunctionCall(<>)),
    Identifier => {
        Box::new(Expression::Variable(Variable{name: <>}))
    },
    // <expr: Expression> <comment: Comment> => Box::new(Expression::ExpressionComment(<expr>, <comment>)),
    // ! => { errors.push(<>); Box::new(Expression::Error) },
};

Block: Block<'input> = {
    "{" <s:(Statement)*> "}" => Block {
        statements: s,
        return_value: None,
    },
    "{" <s:(Statement)*> <e:Expression> "}" => Block {
        statements: s,
        return_value: Some(e),
    }
};

BlockExpr: Box<Expression<'input>> = {
    <b:Block> => Box::new(Expression::Block(b)),
};

FunctionCall: FunctionCall<'input> = {
    <name:Identifier> "(" <args:Comma<Expression>> ")" => FunctionCall {
        name: name,
        args: args,
    }
};


// --------------- math -------------------------------------------------------

Expression: Box<Expression<'input>> = {
    <b:Block> => Box::new(Expression::Block(b)),
    <e:Assignment> => e,
};

Assignment = Tier<AssignmentOp, Comparison>;
Comparison = Tier<ComparisonOp, Express>;
Express = Tier<ExprOp, Factor>;
Factor = Tier<FactorOp, Expr>;

AssignmentOp: Opcode = {
    "=" => Opcode::Assign,
};

ComparisonOp: Opcode = {
    "==" => Opcode::Eq,
    "!=" => Opcode::Ne,
    "<" => Opcode::Lt,
    "<=" => Opcode::Le,
    ">" => Opcode::Gt,
    ">=" => Opcode::Ge,
};

ExprOp: Opcode = {
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
};

FactorOp: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
    "%" => Opcode::Mod,
};


Literal: Literal<'input> = {
    <n:Integer> => Literal::Integer(n),
    <b:Bool> => Literal::Bool(b),
};

Integer: i32 = {
    r"[0-9]+" =>? Ok(i32::from_str(<>).map_err(|err| SolaParseError::NumberError(err))?)
};

Bool: bool = {
    "true" => true,
    "false" => false,
};
